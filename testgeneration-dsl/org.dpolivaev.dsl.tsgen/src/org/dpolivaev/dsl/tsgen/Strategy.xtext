grammar org.dpolivaev.dsl.tsgen.StrategyDsl with org.eclipse.xtext.xbase.Xbase

import 'http://www.eclipse.org/xtext/xbase/Xbase' as xbase

generate strategydsl "http://www.dpolivaev.org/dsl/tsgen/strategydsl"

Generation: {Generation}
	('package' package = QualifiedName)?
	importSection=XImportSection?
	propertyMapping = PropertyMapping?
	(strategies+=Strategy | oracles += Oracle | /* goals += Goal | */ runs += Run)*
;



PropertyMapping:{PropertyMapping}
	'use' userNames += PROPERTY_ID 'as' defaultNames += PROPERTY_ID (','  userNames += PROPERTY_ID 'as' defaultNames += PROPERTY_ID)*
;

	

ID_REST hidden():
	'.' ID | '#' INT
;

PROPERTY_ID hidden():
	ID =>ID_REST* | STRING | '['ID =>ID_REST*']' | '[' STRING ']'
;

Strategy:
	'strategy' name=ID ('extends' baseStrategies += ShortFormStrategyReference ('with' baseStrategies += ShortFormStrategyReference)* )?
		ruleGroups += RuleGroup* 
	;

RuleGroup : {RuleGroup}
	(trigger = Trigger condition = Condition? | condition = Condition) (rule = Rule | '{' ruleGroups += RuleGroup* '}') 
	| rule = Rule
;

Rule returns Rule:
	(
		'let'
		default ?= 'default'? name=PROPERTY_ID
		'be' 
		values = PropertyValues
		(ordered ?= 'ordered' | shuffled ?= 'shuffled')? 
	)
	| skip ?= SkipAction
;

PropertyValues returns Values:
	((name=PROPERTY_ID 'with')?  actions += ValueAction (',' actions += ValueAction)* | 'from' valueReference = [Values]) 
;

Trigger:
	'for' 'each' properties += PROPERTY_ID (',' properties += PROPERTY_ID)*
;

Condition: {Condition}
	'if' expr = XExpression
;

SkipAction : {SkipAction}
	'skip'
;

ValueAction:{ValueAction}
	valueProviders += ValueProvider (=>',' valueProviders += ValueProvider)*
	(=>'{' ruleGroups += RuleGroup* '}')?
;

ValueProvider:
	trace ?= 'traced'?  
	( =>'(' expressions += XExpression+ ')' | expressions += XExpression)
	condition = ValueCondition?   
;

ValueCondition returns Condition:
	'only' 'if' expr = XExpression
;

Run:
	'run' strategies += StrategyReference 
	('with' (strategies += StrategyReference | /* goals += GoalReference | */ oracles += OracleReference) 
	   (',' (strategies += StrategyReference | /* goals += GoalReference | */ oracles += OracleReference))*
	)?
	outputConfiguration = OutputConfiguration?
	reportConfiguration = ReportConfiguration?
	
;
OutputConfiguration:
	('output' xml = STRING ',')? 'apply' xslt=STRING 'output' fileExtension = STRING
	| 'output' xml = STRING
;

ReportConfiguration returns OutputConfiguration:
	'report' (
		(xml = STRING ',')? 'apply' xslt=STRING 'output' fileExtension = STRING | xml = STRING
	)
;

StrategyReference returns StrategyReference: {StrategyReference} 'strategy' goal ?= 'goal'? expr = XExpression;
ShortFormStrategyReference returns StrategyReference: {StrategyReference} expr = XExpression;
// GoalReference : 'goal' expr = XExpression;
OracleReference : 'oracle' goal ?= 'goal'? expr = XExpression;

Oracle:'oracle' name = ValidID
	'{'
		(vars += XVariableDeclaration | subs += MethodDefinition)*
	'}'
;

/*
Goal: CheckList|OpenGoal;

CheckList:
'goal' name = ValidID  expr = XExpression 'matches' item += XExpression (',' item += XExpression)*
;

OpenGoal: 
'open' 'goal' name = ValidID  expr = XExpression
;
 */
 
MethodDefinition:
'def' returnType=JvmTypeReference? name=ValidID 
('(' 
	(parameters+=FullJvmFormalParameter 
		(',' parameters+=FullJvmFormalParameter)*
	)? 
')')? 

(body=XBlockExpression | '=' body = XExpression);

XPrimaryExpression returns xbase::XExpression:
	PropertyCall |
	LabeledExpression |
	XConstructorCall |
	XBlockExpression |
	XSwitchExpression |
	XFeatureCall |
	XLiteral |
	XIfExpression |
	XForLoopExpression |
	XWhileExpression |
	XDoWhileExpression |
	XThrowExpression |
	XReturnExpression |
	XTryCatchFinallyExpression |
	XParenthesizedExpression;


PropertyCall returns xbase::XExpression hidden():
	{PropertyCall}
	':' name = PROPERTY_ID 
;

LabeledExpression returns xbase::XExpression:
	=>({LabeledExpression}
	'[' label = STRING) reason = XExpression?']' (=>expr = XExpression)?
;
