import org.dpolivaev.tsgen.strategies.StrategyHelper

strategy parkCostCalculatorTests 
	extends StrategyHelper.id("testcase")

let script be "parkcalculator/ParkCalculatorTest"
let script.imports be "import static parkcalculator.Lots.*;
import static parkcalculator.BadValues.*;"
let script.driver be "parkcalculator/ParkCalculatorTestDriver"

let foc be "calculate parking costs"{
		let pre1 be "select lot"
		let pre2 be "set entry time"
		let pre3 be "set leaving time"
		let veri be "verify parking costs"
		
		let pre2.entryTime be '"01/1/1970 12:00 am"', '"12/31/1999 12:00 pm"', '"02/28/2016 23:59 pm"'
		
		for each pre1.lot {
			let pre3.weeks be 
				0  { let pre3.days be 0, 1, 6}, 
				1  { let pre3.days be 1, 6},
				6  { let pre3.days be 0, 6},
				26 { let pre3.days be 4}
		}
		
		let pre1.lot be listOfValidLots with 
			"VALET_PARKING"{
				let default dayCosts be ("(7 * ":pre3.weeks" + ":pre3.days") * 18")
				let "parking time" be 
					"0 hours"{
						let pre3.hours be 0
						let pre3.minutes be 0
						let veri.expectedCosts be :dayCosts
					},
					"5 hours or less"{
						let pre3.hours be 0, 4, 5 ordered
						let pre3.minutes be 1, 59, 0 ordered
						let veri.expectedCosts be (:dayCosts " + 12")
					},
					"more than 5 hours"{
						let pre3.hours be 5, 22, 23 ordered
						let pre3.minutes be 1, 0, 59 ordered
						let veri.expectedCosts be (:dayCosts  " + 18")
					}
			}, 
			"SHORT_TERM_PARKING"{
				let default dayCosts be ("(7 * ":pre3.weeks" + ":pre3.days") * 24")
				let "parking time" be 
					"0 hours"{
						let pre3.hours be 0
						let pre3.minutes be 0
						let veri.expectedCosts be :dayCosts
					},
					"1 hour or less"{
						let pre3.hours be 0, 0, 1 ordered
						let pre3.minutes be 1, 59, 0 ordered
						let veri.expectedCosts be (:dayCosts" + 2")
					},
					"11 hours or less with additional 1/2 hour"{
						let pre3.hours be 1, 11 ordered
						let pre3.minutes be 1, 30 ordered
						let veri.expectedCosts be (:dayCosts" + ":pre3.hours" * 2 + 1")
					},
					"11 hours or less with additional hour"{
						let pre3.hours be 1, 11 ordered
						let pre3.minutes be 31, 59 ordered
						let veri.expectedCosts be (:dayCosts" + ":pre3.hours" * 2 + 2")
					},
					"12 hours or more"{
						let pre3.hours be 12, 23 ordered
						let pre3.minutes be 0, 59 ordered
						let veri.expectedCosts be (:dayCosts" + 24")
					}
			},  
			"LONG_TERM_GARAGE_PARKING"{
				let default dayCosts be (:pre3.weeks" * 78 + ":pre3.days" * 13") 
				let "parking time" be 
						"0 hours"{
							let pre3.hours be 0
							let pre3.minutes be 0
							let veri.expectedCosts be :dayCosts
						},
						"6 hour or less"{
							let pre3.hours be 0, 5, 6 ordered
							let pre3.minutes be 1, 59, 0 ordered
							let veri.expectedCosts be (:dayCosts" + ":pre3.hours" * 2 + " if(:pre3.minutes != 0) 2 else 0 )
						},
						"more than 6 hours"{
							let pre3.hours be 6, 23 ordered
							let pre3.minutes be 1, 59 ordered
							let veri.expectedCosts be (:dayCosts" + "13)
						} 
			}, 
			"LONG_TERM_SURFACE_PARKING"{
				let default dayCosts be (:pre3.weeks" * 60 + ":pre3.days" * 10") 
				let "parking time" be 
						"0 hours"{
							let pre3.hours be 0
							let pre3.minutes be 0
							let veri.expectedCosts be :dayCosts
						},
						"5 hour or less"{
							let pre3.hours be 0, 4, 5 ordered
							let pre3.minutes be 1, 59, 0 ordered
							let veri.expectedCosts be (:dayCosts" + ":pre3.hours" * 2 + " if(:pre3.minutes != 0) 2 else 0)
						},
						"more than 5 hours"{
							let pre3.hours be 5, 23 ordered
							let pre3.minutes be 1, 59 ordered
							let veri.expectedCosts be (:dayCosts" + 10")
						} 
			}, 
			"ECONOMY_LOT_PARKING"{
				let default dayCosts be :pre3.weeks as int * 54 + :pre3.days as int * 9
				let "parking time" be 
						"0 hours"{
							let pre3.hours be 0
							let pre3.minutes be 0
							let veri.expectedCosts be :dayCosts
						},
						"4 hour or less"{
							let pre3.hours be 0, 3, 4 ordered
							let pre3.minutes be 1, 59, 0 ordered
							let veri.expectedCosts be :dayCosts as int + :pre3.hours as int * 2 + if(:pre3.minutes != 0) 2 else 0
						},
						"more than 4 hours"{
							let pre3.hours be 4, 23 ordered
							let pre3.minutes be 1, 59 ordered
							let veri.expectedCosts be :dayCosts as int + 9
						} 
			}
		
	}, 
	"select illegal lot"{
		let foc.wrongValue be "NULL_VALUE", "BAD_VALUE"
	}, 
	"use illegal date in calculation"{
		let pre1 be "select lot"
		let default pre1.lot be from listOfValidLots
		let illegalElement be "entry date", "entry time", "entry day part", "leaving date", "leaving time", "leaving day part"
		let pre2 be "set illegal " + :illegalElement
		let pre2.wrongValue be "NULL_VALUE", "BAD_VALUE"
	},
	"use leaving date earlier than entry date"{
		let pre1 be "select lot"
		let default pre1.lot be from listOfValidLots
	}
	
run strategy parkCostCalculatorTests 
	output "output/xml", apply "/java.xslt" output "generated-tests/java" 
	report  "output/report.xml"