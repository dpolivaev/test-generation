grammar org.dpolivaev.dsl.tsgen.StrategyDsl with org.eclipse.xtext.xbase.Xbase

import 'http://www.eclipse.org/xtext/xbase/Xbase' as xbase

generate strategydsl "http://www.dpolivaev.org/dsl/tsgen/strategydsl"

Generation: {Generation}
	('package' package = QualifiedName)?
	importSection=XImportSection?
	(strategies+=Strategy | models += Model | goals += Goal | vars += XVariableDeclaration | subs += MethodDefinition | runs += Run)*
;
	
Strategy:
	'strategy' name=ID ('extends' baseStrategies += ShortFormStrategyReference ('with' baseStrategies += ShortFormStrategyReference)* )?
		ruleGroups += RuleGroup* 
	;

RuleGroup : {RuleGroup}
	(trigger = Trigger condition = Condition? | condition = Condition) (rule = Rule | '{' ruleGroups += RuleGroup* '}') 
	| rule = Rule
;

Rule : PropertyRule|RequirementRule;

PropertyRule returns Rule:
	'let'
	(
		default ?= 'default'? name=PROPERTY_ID
		'be' actions += ValueAction (',' actions += ValueAction)*
		(ordered ?= 'ordered' | shuffled ?= 'shuffled')? 
		| actions += SkipAction name=PROPERTY_ID
	)
;

RequirementRule returns Rule:
	requirement ?= 'cover' name=PROPERTY_ID 'by' actions += ItemAction
;

Trigger:
	'for' 'each' properties += PROPERTY_ID (',' properties += PROPERTY_ID)*
;

Condition:
	'if' expr = XExpression 'then'
;

Action: ValueAction | SkipAction;

SkipAction : {SkipAction}
	'skip'
;

ValueAction:{ValueAction}
	valueProviders += XExpression (=>',' valueProviders += XExpression)*
	('{' ruleGroups += RuleGroup* '}')?
;

ItemAction:{ValueAction}
	valueProviders += XExpression (=>',' valueProviders += XExpression)*
;

Run:
	'run' strategies += StrategyReference 
	('with' strategies += StrategyReference (',' (strategies += StrategyReference | goals += GoalReference))* )?
	outputConfiguration = OutputConfiguration?
	('report' reportConfiguration = OutputConfiguration)?
	
;

OutputConfiguration:
	('output' xml ?= 'xml' ',')? 'apply' xslt=STRING 'output' fileExtension = STRING
;

StrategyReference returns StrategyReference: {StrategyReference} 'strategy' expr = XExpression;
ShortFormStrategyReference returns StrategyReference: {StrategyReference} expr = XExpression;
GoalReference : 'goal' expr = XExpression;

Model:'model' name = ValidID
	'{'
		(vars += XVariableDeclaration | subs += MethodDefinition)*
	'}'
;

Goal: CheckList|OpenGoal;

CheckList:
'goal' name = ValidID  expr = XExpression 'matches' item += XExpression (',' item += XExpression)*
;

OpenGoal: 
'open' 'goal' name = ValidID  expr = XExpression
;

MethodDefinition:
'def' returnType=JvmTypeReference? name=ValidID 
('(' 
	(parameters+=FullJvmFormalParameter 
		(',' parameters+=FullJvmFormalParameter)*
	)? 
')')? 

(body=XBlockExpression | '=' body = XExpression);
	

XPrimaryExpression returns xbase::XExpression:
	XConstructorCall |
	XBlockExpression |
	XSwitchExpression |
	XFeatureCall |
	XLiteral |
	XIfExpression |
	XForLoopExpression |
	XWhileExpression |
	XDoWhileExpression |
	XThrowExpression |
	XReturnExpression |
	XTryCatchFinallyExpression |
	XParenthesizedExpression |
	PropertyCall
	;
	
PropertyCall returns xbase::XExpression hidden():
	{PropertyCall}
	':' name = PROPERTY_ID 
;	

PROPERTY_ID hidden():
	ID (=> '.' ID)* | STRING
;
